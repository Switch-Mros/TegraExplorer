#REQUIRE KEYS
#REQUIRE SD
p=println
pe={pause() exit()}
fwstr={fw=maj.str()+"."+min.str()+"."+pat.str()}
fv={a=readsave("bis:/save/8000000000000120")
	b=a.read("/meta/imkvdb.arc")
	c=["BYTE[]",9,8,0,0,0,0,0,1]
	d=b.find(c)
	if(d>0){
		e=b.slice(d+8,4).project()
		ver=(e[3]<<24)|(e[2]<<16)|(e[1]<<8)|(e[0])
		pat=((ver>>16)&((1<<4)- 1))
		min=((ver>>20)&((1<<6)- 1))
		maj=((ver>>26)&((1<<6)- 1))
	}.else(){print("Firmware nicht gefunden")pe()}
	a=0 fwstr()}
if(mountsys("SYSTEM")){print("SYSTEM EINBINDEN FEHLGESCHLAGEN")pe()}
fv()
sysfw=fw
p("sysMMC Firmware ist",fw)
emufw=""
if (emu()){if(!mountemu("SYSTEM")){fv()emufw=fw p("emuMMC Firmware ist",fw)}}
op=["Beenden","sysMMC dumpen"].copy()
if (emufw!=""){op+"emuMMC dumpen"}
p()r=menu(op,0)clear()
if(r==0){exit()}
if(r==1){fw=sysfw if(mountsys("SYSTEM")){print("SYSTEM EINBINDEN FEHLGESCHLAGEN")pe()}}
if(r==2){fw=emufw if(mountemu("SYSTEM")){print("SYSTEM EINBINDEN FEHLGESCHLAGEN")pe()}}
mkdir("sd:/tegraexplorer")
mkdir("sd:/tegraexplorer/Firmware")
bsp="sd:/tegraexplorer/Firmware/"+fw
if(fsexists(bsp)){p("Verzeichnis existiert bereits! POWER Taste zum ersetzen, jede andere Taste zum Beenden") a=pause() if(!a.power){exit()} deldir(bsp)}
mkdir(bsp)
p("Dumpe nach",bsp)
con=readdir("bis:/Contents/registered")
if(con.result!=0){println("Verzeichnis konnte nicht gelesen werden")pe()}
c=(con.files.len()+con.folders.len()).str()
i=1 f=0 t=timer()
dump={
	fi.foreach("x"){
		if(f){fp="bis:/Contents/registered/"+x+"/00"}
		.else(){fp="bis:/Contents/registered/"+x}
		name=x
		if(ncatype(fp)==1){name=name- 4+".cnmt.nca"}
		color(0x00FF00)
		print("\r["+i.str()+"/"+c+"]", x)
		if(copyfile(fp,bsp+"/"+name)){println("\nFehler waehrend des kopierens")pe()}
		i=i+1
	}
}
fi=con.files dump()
f=1 fi=con.folders dump()
p("\nAbgeschlossen! Benoetigte Zeit",timer()-t/1000,"s")
p("\nBeliebige Taste druecken zum beenden")
pause()